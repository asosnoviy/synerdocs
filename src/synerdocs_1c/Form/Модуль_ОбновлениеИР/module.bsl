
//========================================================================
// Модуль отвечающий за:
//  - выполнение обработчиков при обновлении установленной версии ИР
//========================================================================

// Модули
Перем МенеджерНастроек;

//========================================================================
// ИНИЦИАЛИЗАЦИЯ
//========================================================================

Процедура ПодключитьМодули()
	
	МенеджерНастроек = Модуль("Модуль_МенеджерНастроек");
	
КонецПроцедуры

Процедура Инициализировать() Экспорт
	
	ПодключитьМодули();
	
КонецПроцедуры

//========================================================================
// ЭКСПОРТНЫЙ ИНТЕРФЕЙС
//========================================================================

Процедура ВыполнитьОбработчикиОбновления() Экспорт
	
	СтараяВерсия = ТекущаяВерсия();
	НоваяВерсия = ВерсияИР();
	
	Если НоваяВерсия = СтараяВерсия Тогда
		Возврат;
	КонецЕсли;
	
	ВсеОбработчики = ОбработчикиОбновления();
	ВыполняемыеОбработчики = ОбработчикиОбновленияВИнтервале(ВсеОбработчики, СтараяВерсия, НоваяВерсия);
	
	Для Каждого Обработчик Из ВыполняемыеОбработчики Цикл
		Выполнить("" + Обработчик.ИмяПроцедуры + "()");
	КонецЦикла;
	
	УстановитьТекущуюВерсию(НоваяВерсия);
	
КонецПроцедуры

//========================================================================
// ОБЪЕКТНАЯ МОДЕЛЬ
//========================================================================

Функция Новый_ОбработчикиОбновления()
	
	Обработчики = Новый ТаблицаЗначений;
	
	Колонки = Обработчики.Колонки;
	Колонки.Добавить("Версия");
	Колонки.Добавить("ИмяПроцедуры");
	
	Возврат Обработчики;
	
КонецФункции


//========================================================================
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//========================================================================

Функция ВесВерсии(ВерсияСтрокой)
	
	Версия = СтрРазделить2(ВерсияСтрокой, ".", Истина); //: Версия = Новый Массив;
	
	Вес = 0;
	
	Множитель = 1;
	Сч = Версия.Количество();
	Пока Сч > 0 Цикл
		
		Сч = Сч - 1;
		Попытка
			РазрядВерсии = Число(Версия[Сч]);
		Исключение
			РазрядВерсии = 0;
		КонецПопытки;
		
		Вес = Вес + РазрядВерсии * Множитель;
		
		Множитель = Множитель * 1000;
		
	КонецЦикла;
	
	Возврат Вес;
	
КонецФункции

//===================================
//{ Хранение текущей версии

Функция ХранилищеВерсии()
	
	Возврат "Syn_ТекущаяВерсия";
	
КонецФункции

Функция ТекущаяВерсия()
	
	Версия = ВосстановитьЗначение(ХранилищеВерсии());
	
	Возврат Версия;
	
КонецФункции

Функция УстановитьТекущуюВерсию(НовоеЗначение)
	
	СохранитьЗначение(ХранилищеВерсии(), НовоеЗначение);
	
КонецФункции

//}

//===================================
//{ Методы работы с Новый_ОбработчикиОбновления()

Функция ОбработчикиОбновленияВИнтервале(Обработчики, ВерсияОт, ВерсияДо)
	
	Результат = Новый_ОбработчикиОбновления(); //: Результат = Новый ТаблицаЗначений;
	
	ВесОт = ВесВерсии(ВерсияОт);
	ВесДо = ВесВерсии(ВерсияДо);
	
	Для Каждого Обработчик Из Обработчики Цикл
		
		ВесОбработчика = ВесВерсии(Обработчик.Версия);
		Если ВесОбработчика > ВесОт И ВесОбработчика <= ВесДо Тогда
			ЗаполнитьЗначенияСвойств(Результат.Добавить(), Обработчик);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗарегистрироватьОбработчик(Обработчики, Версия, ИмяПроцедуры)
	
	//: Обработчики = Новый ТаблицаЗначений;
	
	НовыйОбработчик = Обработчики.Добавить();
	НовыйОбработчик.Версия			= Версия;
	НовыйОбработчик.ИмяПроцедуры	= ИмяПроцедуры;
	
КонецПроцедуры

//}

//===================================
//{ Обработчики обновления

Функция ОбработчикиОбновления()
	
	Обработчики = Новый_ОбработчикиОбновления();

	Версия = "1.21.0.1";
	ЗарегистрироватьОбработчик(Обработчики, Версия, "ОбновитьНастройки_1_21_0");
	
	Версия = "1.21.0.20";
	ЗарегистрироватьОбработчик(Обработчики, Версия, "ОбновитьНастройки_1_21_0_20");
	
	Возврат Обработчики;
	
КонецФункции

Процедура ОбновитьНастройки_1_21_0()
	
	Настройки = МенеджерНастроек.НастройкиИР();
	НастройкиПоУмолчанию = МенеджерНастроек.НастройкиПоУмолчанию();
	
	//{ Удален вариант настройки "Спрашивать"
	
	ПоддержкаНовыхФорматов = Настройки.ПоддержкаНовыхФорматов;
	
	ВариантыОтправки = МенеджерНастроек.ВариантыОтправкиУниверсальногоДокумента();
	
	Если НЕ ЗначениеПринадлежитПеречислению(ПоддержкаНовыхФорматов.ВариантОтправкиУниверсальногоДокумента, ВариантыОтправки) Тогда
		ПоддержкаНовыхФорматов.ВариантОтправкиУниверсальногоДокумента = НастройкиПоУмолчанию.ПоддержкаНовыхФорматов.ВариантОтправкиУниверсальногоДокумента;
	КонецЕсли;
	
	//}
	
	МенеджерНастроек.СохранитьНастройкиИР(Настройки);
	
КонецПроцедуры

Процедура ОбновитьНастройки_1_21_0_20()
	
	Настройки = МенеджерНастроек.НастройкиИР();
	НастройкиПоУмолчанию = МенеджерНастроек.НастройкиПоУмолчанию();
	
	//{ Новый путь публикации сервиса
	
	Если МенеджерНастроек.ЭтоПродуктив(Настройки) Тогда
		Настройки.ПутьПубликацииСервиса = НастройкиПоУмолчанию.ПутьПубликацииСервиса;
	КонецЕсли;
	
	//}
	
	МенеджерНастроек.СохранитьНастройкиИР(Настройки);
	
КонецПроцедуры

//}
