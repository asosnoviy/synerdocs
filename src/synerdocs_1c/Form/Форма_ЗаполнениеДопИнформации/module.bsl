
//========================================================================
// Форма заполнения дополнительных реквизитов ЭД.
// Используется для заполнения объекта Модуль_ОбъектнаяМодель.Новый_ИнфПоле().
//========================================================================

Перем _Параметры;

Перем КлючСтроки;

// Модули
Перем ОбъектнаяМодель;

//========================================================================
// ИНИЦИАЛИЗАЦИЯ
//========================================================================

Процедура ПодключитьМодули()
	
	ОбъектнаяМодель = Модуль("Модуль_ОбъектнаяМодель");
	
КонецПроцедуры

Процедура Инициализировать()
	
	ПодключитьМодули();
	
КонецПроцедуры

Процедура ИнициализироватьФорму()
	
	Параметры = ПараметрыФормы();
	
	КлючСтроки = Параметры.КлючСтроки;
	
	УстановитьИнфПоле(Параметры.Модель);
	
КонецПроцедуры

Функция ПараметрыФормы()
	
	Если _Параметры = Неопределено Тогда
		ВызватьИсключение "Параметры формы не установлены";
	КонецЕсли;
	
	Возврат _Параметры;
	
КонецФункции

//========================================================================
// ЭКСПОРТНЫЙ ИНТЕРФЕЙС
//========================================================================

Функция Параметры() Экспорт
	
	Параметры = Новый Структура(
		"Модель,
		|КлючСтроки"
	);
	
	Параметры.Модель		= Неопределено; //: Модуль_ОбъектнаяМодель.Новый_ИнфПоле();
	Параметры.КлючСтроки	= Неопределено;
	
	Возврат Параметры;
	
КонецФункции

Функция УстановитьПараметры(Параметры) Экспорт
	
	_Параметры = ФиксированныеДанные(Параметры);
	
КонецФункции

Функция ПроверяемыеРеквизиты() Экспорт
	
	Поля = Новый СписокЗначений;
	
	ПредставлениеДопИнфо = НСтр("ru = 'Дополнительная информация'");
	Поля.Добавить("ДополнительнаяИнформация.Поле",        ПредставлениеДопИнфо + НСтр("ru = '#Поле'"));
	Поля.Добавить("ДополнительнаяИнформация.Значение",    ПредставлениеДопИнфо + НСтр("ru = '#Значение'"));
	
	Возврат Поля;
	
КонецФункции

//========================================================================
// ОБЪЕКТНАЯ МОДЕЛЬ
//========================================================================

Функция Новый_Результат()
	
	Результат = Новый Структура(
		"КлючСтроки,
		|Модель"
	);
	
	Результат.КлючСтроки	= Неопределено;
	Результат.Модель		= Неопределено; //: Модуль_ОбъектнаяМодель.Новый_ИнфПоле();
	
	Возврат Результат;
	
КонецФункции

//========================================================================
// СОБЫТИЯ
//========================================================================

//===================================
//{ События формы

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьФорму();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) Экспорт
	
КонецПроцедуры

//}

//===================================
//{ События табличной части ДополнительнаяИнформация

Процедура ДополнительнаяИнформацияПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	НомерСтроки = ДополнительнаяИнформация.Индекс(ДанныеСтроки) + 1;
	ОформлениеСтроки.Ячейки.НомерСтроки.УстановитьТекст(Формат(НомерСтроки, "ЧГ="));
	
КонецПроцедуры

//}

//===================================
//{ Кнопки формы

Процедура ОсновныеДействияФормыСохранить(Кнопка)
	
	Если НЕ ПроверитьЗаполнениеФормы(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Сохранить();
	
КонецПроцедуры

//}

//===================================
//{ Команды

Процедура Сохранить()
	
	Результат = Новый_Результат();
	Результат.КлючСтроки	= КлючСтроки;
	Результат.Модель		= ИнфПоле();
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

//}

//========================================================================
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//========================================================================

//===================================
//{ Геттеры и сеттеры

// Модель формы

Процедура УстановитьИнфПоле(ИнфПоле)
	
	Для Каждого СтрокаИнфПоле Из ИнфПоле Цикл
		НовоеПоле = ДополнительнаяИнформация.Добавить();
		НовоеПоле.Поле		= СтрокаИнфПоле.Идентификатор;
		НовоеПоле.Значение	= СтрокаИнфПоле.Значение;
	КонецЦикла;
	
КонецПроцедуры

Функция ИнфПоле()
	
	ИнфПоле = ОбъектнаяМодель.Новый_ИнфПоле();
	
	Для Каждого СтрокаДопИнфо Из ДополнительнаяИнформация Цикл
		НовоеПоле = ИнфПоле.Добавить();
		НовоеПоле.Идентификатор	= СтрокаДопИнфо.Поле;
		НовоеПоле.Значение		= СтрокаДопИнфо.Значение;
	КонецЦикла;
	
	Возврат ИнфПоле;
	
КонецФункции

//}


Инициализировать();

