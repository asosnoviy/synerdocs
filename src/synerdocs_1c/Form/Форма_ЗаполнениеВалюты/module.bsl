
//========================================================================
// Форма заполнения реквизитов валюты.
// Используется для заполнения объекта Модуль_ОбъектнаяМодель.Новый_Валюта().
//========================================================================

Перем _Параметры;

// Модули
Перем ОбъектнаяМодель, Классификаторы;

//========================================================================
// ИНИЦИАЛИЗАЦИЯ
//========================================================================

Процедура ПодключитьМодули()
	
	ОбъектнаяМодель	= Модуль("Модуль_ОбъектнаяМодель");
	Классификаторы	= Модуль("Модуль_Классификаторы");
	
КонецПроцедуры

Процедура Инициализировать()
	
	ПодключитьМодули();
	
КонецПроцедуры

Процедура ИнициализироватьФорму()
	
	Параметры = ПараметрыФормы();
	
	ЭлементыФормы.Код.СписокВыбора = СписокВыбораВалюты();
	
	УстановитьВалюту(Параметры.Модель);
	
КонецПроцедуры

Функция ПараметрыФормы()
	
	Если _Параметры = Неопределено Тогда
		ВызватьИсключение "Параметры формы не установлены";
	КонецЕсли;
	
	Возврат _Параметры;
	
КонецФункции

//========================================================================
// ЭКСПОРТНЫЙ ИНТЕРФЕЙС
//========================================================================

Функция Параметры() Экспорт
	
	Параметры = Новый Структура(
		"Модель"
	);
	
	Параметры.Модель = Неопределено; //: Модуль_ОбъектнаяМодель.Новый_Валюта();
	
	Возврат Параметры;
	
КонецФункции

Функция УстановитьПараметры(Параметры) Экспорт
	
	_Параметры = ФиксированныеДанные(Параметры);
	
КонецФункции

Функция ПроверяемыеРеквизиты() Экспорт
	
	Поля = Новый СписокЗначений;
	
	Поля.Добавить("Код", НСтр("ru = 'Валюта'"));
	
	Возврат Поля;
	
КонецФункции

//========================================================================
// СОБЫТИЯ
//========================================================================

//===================================
//{ События формы

Процедура ПередОткрытием(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьФорму();
	
КонецПроцедуры

Процедура ПриОткрытии()
	
	УстановитьОтображение();
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты) Экспорт
	
	
	
КонецПроцедуры

//}

//===================================
//{ Кнопки формы

Процедура ОсновныеДействияФормыСохранить(Кнопка)
	
	Если НЕ ПроверитьЗаполнениеФормы(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Сохранить();
	
КонецПроцедуры

//}

//===================================
//{ События элементов формы

Процедура КодПриИзменении(Элемент)
	
	УстановитьКод(Код);
	
КонецПроцедуры

Процедура КурсПриИзменении(Элемент)
	
	УстановитьКурс(Курс);
	
КонецПроцедуры

//}

//===================================
//{ Команды

Процедура Сохранить()
	
	Результат = Валюта();
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

//}

//========================================================================
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//========================================================================

Процедура УстановитьОтображение()
	
	ШаблонПредставление = НСтр("ru = '%1 (%2)
                                |1 %3 = %4 RUB'");
								
	Представление = СтрШаблон2(ШаблонПредставление,
		Наименование, Код,
		Обозначение, Курс
	);
	ЭлементыФормы.НадписьПредставление.Заголовок = Представление;
	
КонецПроцедуры

//===================================
//{ Геттеры и сеттеры

// Модель валюты

Процедура УстановитьВалюту(НовоеЗначение)
	
	Если НовоеЗначение = Неопределено Тогда
		Валюта = ОбъектнаяМодель.Новый_Валюта();
	Иначе
		Валюта = НовоеЗначение;
	КонецЕсли;
	
	УстановитьКод(Валюта.Код);
	УстановитьКурс(Валюта.Курс);
	
КонецПроцедуры

Функция Валюта()
	
	Валюта = ОбъектнаяМодель.Новый_Валюта();
	
	Валюта.Код			= Код;
	Валюта.Наименование	= Наименование;
	
	Валюта.Курс			= Курс;
	
	Возврат Валюта;
	
КонецФункции

// Шапка

Процедура УстановитьКод(НовоеЗначение)
	
	Код = НовоеЗначение;
	
	Если ЗначениеЗаполнено(Код) Тогда
		ВалютаИзКлассификатора = Классификаторы.Валюта(Код);
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ВалютаИзКлассификатора, "Код, Наименование, Обозначение");
	КонецЕсли;
	
	УстановитьОтображение();
	
КонецПроцедуры

Процедура УстановитьКурс(НовоеЗначение)
	
	Курс = НовоеЗначение;
	
	УстановитьОтображение();
	
КонецПроцедуры

//}

Функция СписокВыбораВалюты()
	
	СписокВыбора = Новый СписокЗначений;
	
	ВсеВалюты = Классификаторы.ВсеВалюты();
	Для Каждого Валюта Из ВсеВалюты Цикл
		СписокВыбора.Добавить(Валюта.Код, Валюта.Обозначение);
	КонецЦикла;
	
	Возврат СписокВыбора;
	
КонецФункции



Инициализировать();

