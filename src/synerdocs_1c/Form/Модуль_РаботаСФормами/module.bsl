
//========================================================================
// Модуль отвечающий за работу с элементами форм.
//========================================================================

// Модули
Перем ОбъектнаяМодель;

//========================================================================
// ИНИЦИАЛИЗАЦИЯ
//========================================================================

Процедура ПодключитьМодули()
	
	ОбъектнаяМодель = Модуль("Модуль_ОбъектнаяМодель");
	
КонецПроцедуры

Процедура Инициализировать() Экспорт
	
	ПодключитьМодули();
	
КонецПроцедуры

//========================================================================
// ОБЪЕКТНАЯ МОДЕЛЬ
//========================================================================

Функция ИзмененныеРеквизитыТЧ() Экспорт
	
	РеквизитыТЧ = Новый Структура(
		"Цена,
		|Количество,
		|СтавкаНДС,
		|СуммаБезНДС,
		|СуммаСНДС"
	);
	
	Для Каждого Элемент Из РеквизитыТЧ Цикл
		РеквизитыТЧ[Элемент.Ключ] = Элемент.Ключ;
	КонецЦикла;
	
	РеквизитыТЧ = Новый_Перечисление(РеквизитыТЧ);
	
	Возврат РеквизитыТЧ;
	
КонецФункции

Функция Новый_РеквизитыТЧ() Экспорт
	
	РеквизитыТЧ = Новый Структура(
		"Цена,
		|Количество,
		|СтавкаНДС,
		|СуммаБезНДС,
		|СуммаСНДС,
		|СуммаНДС"
	);
	
	РеквизитыТЧ.Цена		= 0;
	РеквизитыТЧ.Количество	= 0;
	РеквизитыТЧ.СтавкаНДС	= Неопределено; //: Модуль_ОбъектнаяМодель.СтавкиНДС();
	РеквизитыТЧ.СуммаБезНДС	= 0;
	РеквизитыТЧ.СуммаСНДС	= 0;
	РеквизитыТЧ.СуммаНДС	= 0;
	
	Возврат РеквизитыТЧ;
	
КонецФункции

//========================================================================
// ЭКСПОРТНЫЙ ИНТЕРФЕЙС
//========================================================================

Процедура ПересчитатьЗначенияРеквизитовТЧ(РеквизитыТЧ, ЧтоИзменилось) Экспорт
	
	//: РеквизитыТЧ = Документы.РеализацияТоваровУслуг.ПустаяСсылка().Товары[0];
	
	ЧтоМоглоИзмениться = ИзмененныеРеквизитыТЧ();
	
	ПересчитатьЦену = Ложь;
	ПересчитатьСуммуНДС = Ложь;
	ПересчитатьСуммуСНДС = Ложь;
	
	Если ЧтоИзменилось = ЧтоМоглоИзмениться.Количество
			ИЛИ ЧтоИзменилось = ЧтоМоглоИзмениться.Цена Тогда
			
		РеквизитыТЧ.СуммаБезНДС = РеквизитыТЧ.Цена * РеквизитыТЧ.Количество;
		ПересчитатьСуммыВСтрокеТЧ(РеквизитыТЧ);
		
	ИначеЕсли ЧтоИзменилось = ЧтоМоглоИзмениться.СтавкаНДС Тогда
		
		ПересчитатьСуммыВСтрокеТЧ(РеквизитыТЧ);
		
	ИначеЕсли ЧтоИзменилось = ЧтоМоглоИзмениться.СуммаБезНДС Тогда
		
		ПересчитатьСуммыВСтрокеТЧ(РеквизитыТЧ);
		ПересчитатьЦенуВСтрокеТЧ(РеквизитыТЧ);
		
	ИначеЕсли ЧтоИзменилось = ЧтоМоглоИзмениться.СуммаСНДС Тогда
		
		ПересчитатьСуммыВСтрокеТЧ(РеквизитыТЧ, Ложь);
		ПересчитатьЦенуВСтрокеТЧ(РеквизитыТЧ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьАвтоОтметкуНезаполненного(Элемент, Значение) Экспорт
	
	Элемент.АвтоОтметкаНезаполненного = Значение;
	Если НЕ Значение Тогда
		Элемент.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
КонецПроцедуры

//========================================================================
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//========================================================================

Процедура ПересчитатьСуммыВСтрокеТЧ(РеквизитыТЧ, ОтСуммыБезНДС = Истина)
	
	//: РеквизитыТЧ = Документы.РеализацияТоваровУслуг.ПустаяСсылка().Товары[0];
	
	Если ОтСуммыБезНДС Тогда
		
		РеквизитыТЧ.СуммаНДС = РассчитатьСуммуНДС(РеквизитыТЧ.СуммаБезНДС, Ложь, РеквизитыТЧ.СтавкаНДС);
		РеквизитыТЧ.СуммаСНДС = РеквизитыТЧ.СуммаБезНДС + РеквизитыТЧ.СуммаНДС;
		
	Иначе
		
		РеквизитыТЧ.СуммаНДС = РассчитатьСуммуНДС(РеквизитыТЧ.СуммаСНДС, Истина, РеквизитыТЧ.СтавкаНДС);
		РеквизитыТЧ.СуммаБезНДС = РеквизитыТЧ.СуммаСНДС - РеквизитыТЧ.СуммаНДС;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПересчитатьЦенуВСтрокеТЧ(РеквизитыТЧ)
	
	//: РеквизитыТЧ = Документы.РеализацияТоваровУслуг.ПустаяСсылка().Товары[0];
	
	РеквизитыТЧ.Цена = РассчитатьЦену(РеквизитыТЧ.СуммаБезНДС, РеквизитыТЧ.Количество);
	
КонецПроцедуры


Функция РассчитатьЦену(Сумма, Количество)
	
	Цена = Сумма / ЗаполненноеЗначение(Количество, 1);
	
	Возврат Цена;
	
КонецФункции

Функция РассчитатьСуммуНДС(Сумма, СуммаВключаетНДС, СтавкаНДС)
	
	СуммаНДС = Неопределено;
	
	РазмерСтавки = ОбъектнаяМодель.РазмерСтавкиНДС(СтавкаНДС);
	
	Если РазмерСтавки = Неопределено Тогда
		
		СуммаНДС = 0;
		
	ИначеЕсли РазмерСтавки = 0 Тогда
		
		СуммаНДС = 0;
		
	ИначеЕсли СуммаВключаетНДС Тогда
		
		СуммаБезНДС = Сумма * 100 / (100 + РазмерСтавки);
		СуммаНДС = Сумма - СуммаБезНДС;
		
	ИначеЕсли НЕ СуммаВключаетНДС Тогда
		
		СуммаБезНДС = Сумма;
		СуммаНДС = СуммаБезНДС * РазмерСтавки / 100;
		
	КонецЕсли;
	
	Возврат СуммаНДС;
	
КонецФункции
